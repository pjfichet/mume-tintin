#nop leader

#var {leader} {none};
#alias {leader} {
	#if {"%0" == "none"} {
		leader_unhighlight;
		#var {leader} {none};
		#echo {Leader cleared.};
	};
	#elseif {"%0" != ""} {
		leader_unhighlight;
		#var {leader} {%0};
		#echo {Leader set to $leader\.};
		leader_highlight;
	};
	#else {
		#if {"$leader" == "none"} {#echo Leader undefined.};
		#else {#echo {Your leader is $leader\.}};
	};
};

#alias {leader_highlight} {
	#nop need '%%', otherwise sub uses the function parameter;
	#sub {^$leader leaves {north|south|west|east|up|above|down|below}} {$leader leaves <039>%%1<099>};
	#sub {^{[^/]*} sends $leader sprawling with a powerful bash.$} {%1 sends $leader <039>sprawling<099> with a powerful <039>bash<099>.};
	#sub {^$leader panics, and attempts to flee.$} {$leader <039>panics<099> and attempts to flee.};
	#sub {^$leader dismounts} {$leader <039>dismounts<099>};
	#sub {^$leader picks up} {$leader <039>picks up<099>};
	#highlight {fighting $leader} {yellow};
}

#alias {leader_unhighlight} {
	#unsubstitute {^{$leader} leaves {north|south|west|east|up|above|down|below}};
	#unsubstitute {^{[^/]*} sends $leader sprawling with a powerful bash.$};
	#unsubstitute {^$leader panics and attempts to flee.$};
	#unsubstitute {^$leader dismounts};
	#unsubstitute {^$leader picks up};
	#unhighlight {fighting $leader};
}

#action {^{\* transfers group leadership to you\.|You leave the group\.|Group disbanded\.|What group\?\!|You are no longer a member of the group\!|You can\'t group with yourself\.}$} {leader none}

#action {^%* disbands {his|her} group.$} {
	#if {"%1" == "$leader"} {leader none}
}
#action {^You now follow %*.$} {leader %1}
#action {^You are now a member of %*'s group.$} {leader %1}
#action {^You transfer group leadership to %*.$} {leader %1}
#action {^     %* (Head of group)$} {#var {leader} {%1}}

