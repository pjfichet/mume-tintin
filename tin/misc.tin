#alias showsend {
	#send {%0};
	#showme {<109>%0<099>};
};
#alias {date} {
	#system {date};
}
#alias {src} {#read $src/%1.tin};

quickref_alias {sellall} {
	#var {selllist} {%0};
	#replace {selllist} { } {;};
	#foreach {$selllist} {i} {sell all.$i};
} {sell all.arguments};

#alias {mount} {
	#var {mount} {%1};
	#showme {mount: $mount.};
}

quickref_alias {flask} {
	#var {flask} {%1};
	#showme {flask: $flask.};
} {set water container.};

quickref_alias bag {
	#var {bag} {%1};
	#showme {bag: $bag.};
} {set container.};

quickref_alias {weapon} {
	#if {"$weapon" != "%1"} {
		get %1 $bag;
		draw %1;
		put $weapon $bag;
		#var {weapon} {%1};
	};
} {draw %1 from $bag; set default weapon};

quickref_alias {piercing} {
	#if { "%1" != ""} {
		#var {piercing} {%1};
	};
	#showme {piercing: $piercing.};
} {set piercing weapon};

quickref_alias {shooting} {
	#if { "%1" != ""} {
		#var {shooting} {%1};
	}
	#showme {shooting: $shooting.};
} {set shooting weapon};

quickref_alias mount {
	#var {mount} {%1};
	#showme {mount: $mount.};
} {set mount};

quickref_alias {mood} {
	#if { "%0" != ""} {
		#var {wimpy} {%0};
		#nop echo {Change wimpy $wimpy\.};
		#send {change wimpy $wimpy};
	};
	#else {
		#if { "$vitals[mood]" == "wimpy"} {
			#var {vitals[mood]} {aggressive};
			#echo {Change mood aggressive: $wimpy\.};
			change mood aggressive;
		};
		#elseif { "$vitals[mood]" == "aggressive"} {
			#var {vitals[mood]} {wimpy};
			#echo {Change mood wimpy: $wimpy\.};
			change mood wimpy;
		};
	};
} {toggle mood, set wimpy %1};

quickref_alias {rec} {
	#if {"%0" != ""} {
		#var {reclist} {%0};
		#replace {reclist} { } {;};
	};
	#elseif {"$reclist" != ""} {
		#showme {<109>$reclist<099>};
		#foreach {$reclist} {i} {$i};
	};
	#else {#showme {Nothing is recorded.}};
} {record a list of actions or execute recorded list.};

#alias {rent} {
	#if { "%0" == "" } {
		info;
		xp;
		train all;
		#action {^None of your skills needed refreshing.$} {#send {rent}};
		#action {^No more skills need refreshing.$} {#send {rent}};
	};
	#else {
		#send {rent %0};
	};
};

#alias {report} {
	parse_prompt;
	#if {"%1" == ""} {#var {report} {#showme}};
	#else {#var {report} {%1}};
	#if {"$vitals[opponent-hits]" == ""} {$report hp: $vitals[hp-string], mana: $vitals[mana-string], mp: $vitals[mp-string].};
	#else {$report hp: $vitals[hp-string], mana: $vitals[mana-string], mp: $vitals[mp-string], vs: $vitals[opponent-hits].};
};

quickref_alias {chest} {
	#if {"%0" != ""} {
		#showme {<109>open chest\; look in chest\; get %1 chest<099>};
		#send {open chest};
		#send {look in chest};
		#send {get %1 chest};
	};
	#else {
		#showme {open chest\; look in chest};
		#send {open chest};
		#send {look in chest};
	};
} {look in chest or get %1 from chest.};

#alias {castfood} {
	#action {^{A magic mushroom|Something} suddenly appears.$} {
		eat mushroom;
		#unaction {^{A magic mushroom|Something} suddenly appears.$};
	};
	showsend cast 'create food';
}

quickref_alias {scroll} {
	showsend get %1 all;
	showsend recite %1 %2;
} {get %1 all; recite %1 %2};

quickref_alias {eemote} {
	#var {emote} {%0};
	#showme {<069>$name $emote<099>};
	#send {emote $emote};
} {emote %0};


quickref_alias burnweb {
	rem $weapon;
	rem lantern;
	pour lantern web;
	burn web;
	wear lantern belt;
	wield $weapon;
} {use lantern to burn web.};

quickref_alias {willow} {
	#showme hunting place:w 1:n 2:w 3:w 4:s 5:w 6:w 7:n 8:w dangerous:n deep:e deep:e willow.
} {path to willow from hunting place};

quickref_alias {configure} {
	change autoride on
	change spam speech off;
	change spam description off;
	change spam fight off;
	change spam group off;
	change spam language off;
	change spam mount on;
	change spam weapon on;
	change spam detect on;
	change spam alias on;
	change compact on;
	change wordwrap off;
	change colour achievement magenta;
	change colour avoid-damage none;
	change colour character none;
	change colour code none;
	change colour damage none;
	change colour emote none;
	change colour emphasis none;
	change colour enemy bold white;
	change colour exits none;
	change colour header green;
	change colour hit none;
	change colour look green;
	change colour magic magenta;
	change colour miss none;
	change colour movement-in none;
	change colour movement-out none;
	change colour narrate none;
	change colour normal none;
	change colour pray none;
	change colour prompt none;
	change colour room-desc none;
	change colour say none;
	change colour shout none;
	change colour social cyan;
	change colour song bold cyan;
	change colour status green;
	change colour tell none;
	change colour weather cyan;
	change colour yell bold cyan;
	change colour zone-bad yellow;
	change colour zone-evil red;
	change colour zone-good green;
	change colour zone-neutral white;
	change colour zone-nice cyan;
} {change spam and colour.};

