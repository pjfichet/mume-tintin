#alias {split} {
	#nop this function can be overwritten in sidebar.tin;
	#event {CATCH SCREEN SPLIT FILL} {#nop};
	#split 0 2;
	#screen clear all;
};

#nop gag the prompt
#gag {^>$}

#nop replace server prompt
#action {^>$} {
	#if {"$vitals[ride]" == "true"} {#var {prompt[ride]} {R}};
	#else {#var {prompt[ride]} {}};

	#if {"$light" == "on"} {#var {prompt[light]} {<149>!<099>}};
	#else {#var {prompt[light]} {<279>o<099>}};


	#if {"$vitals[hidden]" == "true"} {
		#var {prompt[sneak]} {$vitals[sneak]H};
	};
	#else {
		#var {prompt[hidden]} {};
		#var {prompt[sneak]} {<019>$vitals[sneak]<099>};
	};

	#format {prompt[state]} {%s%s%s hp:%s mp:%s ma:%s > %s}
		{$prompt[light]}
		{$prompt[sneak]}
		{$prompt[ride]}
		{$vitals[hp-string]}
		{$vitals[mp-string]}
		{$vitals[mana-string]}
		{$target};

	#if {"$vitals[light]" == "!"} {#var {prompt[roomlight]} {<149>!<099>}};
	#elseif {"$vitals[light]" == "*"} {#var {prompt[roomlight]} {<039>*<099>}};
	#elseif {"$vitals[light]" == ")"} {#var {prompt[roomlight]} {<179>)<099>}};
	#elseif {"$vitals[light]" == "o"} {#var {prompt[roomlight]} {<279>o<099>}};


	#if {"$vitals[mount-moves]" == ""} {#var {prompt[mount]} {}};
	#else {#var {prompt[mount]} { mount:$vitals[mount-moves]}};

	#if {"$vitals[buffer]" == ""} {#var {prompt[buffer]} {}};
	#else {#var {prompt[buffer]} { $vitals[buffer]:$vitals[buffer-hits]}};

	#if {"$vitals[opponent]" == ""} {#var {prompt[opponent]} {}};
	#else {#var {prompt[opponent]} { > $vitals[opponent]:$vitals[opponent-hits]}};

	#format {prompt[room]} {%s%s %s%s%s}
		{$prompt[roomlight]}
		{$vitals[fog]}
		{$prompt[mount]}
		{$prompt[buffer]}
		{$prompt[opponent]};

	#if {$sidebar == 1} {sidebar};

	#showme {$prompt[room]} {-3};
	#showme {$prompt[state]} {-2};
};

