#nop scout.tt

#var {auto_light} {0}
#alias autolight {
    #if {$auto_light == 0} {#var {auto_light} {1}};
    #else {#var {auto_light} {0}};
};

#var {lightsource} {lantern}
#alias {lightsource} {
	#var {lightsource} {%0};
	#echo {Lightsource: $lightsource\.};
};

#var {light} {on}
#alias {lighton} {
	#if { "$light" == "off" } {
		#if { "$lightsource" == "lantern" } {showsend light lantern};
		#else {showsend uncover $lightsource};
		#var {light} {on};
	};
};

#alias {lightoff} {
	#if {"$light" == "on" } {
		#if {"$lightsource" == "lantern"} {showsend snuff lantern};
		#else {showsend cover $lightsource};
		#var {light} {off};
	};
};

#alias lightor {
	#if {"$vitals[light]" == "o"} {lighton};
};

#alias togglelight {
   #if {"$light" == "off"} {lighton};
   #else {lightoff};
};

#alias lightoff_sneak {
    #if {"$vitals[sneak]" != ""} {lightoff};
};

#action {^Your light has gone out.$} {#var {light} {off}}

#action {^The day has begun.$} {
    #if {$auto_light == 1} {lightoff};
};

#action {^The night has begun.$} {
    #if {$auto_light == 1} {lighton};
};

#action {^You attempt to hide yourself.$} {
	#action {^Ok.$} {
		#var {light} {off};
		#unaction {^Ok.$};
	};
	#delay {13} {#unaction {^Ok.$}};
};

#action {^Hmm... It seems there is no sign of %* here.} {lightoff_sneak}
#action {^Hmm... you have lost the track.} {lightoff_sneak}
#action {^You stop searching} {lightoff_sneak}
#action {^You don't see anything special there...} {lightoff_sneak}
#action {^You skillfully discover hidden %*} {lightoff_sneak}
#action {^You found {a|an} %*.} {lightoff_sneak}
